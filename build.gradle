apply plugin: 'java'

def projectVersion = "1.10-SNAPSHOT"

subprojects {
    apply plugin: 'java'
	
    version = "$projectVersion"
	
	sourceCompatibility = '1.6'
	targetCompatibility = '1.6'
	
	sourceSets {
		main { 
			java { 
				srcDir 'src' 
				srcDir 'src-gen' 
			} 
			resources {
				srcDir 'src-resources'
			}
		}
		test {
			java {
				srcDir 'test-gen' 
				srcDir 'test' 
			} 
			resources {
				srcDir 'test-resources'
			}
		}
	}
	
	test {
		ignoreFailures = true
	}

	apply plugin: 'pmd'
	pmd {
		ignoreFailures = true

		ruleSets = []
		ruleSetFiles = files("../lib/static-analysis/pmd-5.0.5/pmdrules.xml")
	}
	
	apply plugin: 'checkstyle'
	checkstyle {
		ignoreFailures = true
		showViolations = false
		
		configFile = file("../lib/static-analysis/checkstyle-5.6/cs-conf.xml")		
	}

	apply plugin: 'findbugs'
	findbugs {
		ignoreFailures = true 
		effort = "max"
		reportLevel = "low"
		excludeFilter = file("../lib/static-analysis/findbugs-2.0.3/fb-filter.xml")
	}

	dependencies {
		pmd fileTree(dir: '../lib/static-analysis/pmd-5.0.5/lib', include: '*.jar')
		checkstyle fileTree(dir: '../lib/static-analysis/checkstyle-5.6/lib', include: '*.jar')
		findbugs fileTree(dir: '../lib/static-analysis/findbugs-2.0.3/lib', include: '*.jar')
	}
	
}

def today() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}

task mainJar( type: Jar, dependsOn:  subprojects.tasks["build"] ) {
	baseName = "kieker-$projectVersion"
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
	
	manifest {
		attributes (
			"Specification-Version": "${projectVersion}",
			"Specification-Vendor": "Kieker Project",
			"Implementation-Title": "Kieker Framework",
			"Implementation-Version": "${projectVersion} (" + today() + ")",
			"Implementation-Vendor": "Kieker Project",
			"kieker"
		)
    }
}

task emfJar( type: Jar, dependsOn:  subprojects.tasks["build"] ) {
	baseName = "kieker-${projectVersion}_emf"
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}

task aspectJJar( type: Jar, dependsOn:  subprojects.tasks["build"] ) {
	baseName = "kieker-${projectVersion}_aspectj"
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}